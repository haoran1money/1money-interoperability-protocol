set shell := ["bash", "-c"]

# One-shot target for pre-commit checks
precommit: fmt-check lint build check-tests

# Check formatting of all project files
fmt-check:
	cargo +nightly fmt --all --check
	taplo fmt --check
	deno fmt --check ../.github/**/*.yaml ../.github/**/*.yml

# Format all project files
fmt:
	cargo +nightly fmt --all
	taplo fmt
	deno fmt ../.github/**/*.yaml ../.github/**/*.yml

# Lint all components
lint:
	cargo +nightly clippy --tests -- -D warnings
	forge lint --force

# Build artifacts
build:
	cargo build --workspace
	forge build --force

# Run test suites
check-tests:
	cargo nextest run --workspace
	forge test --force
